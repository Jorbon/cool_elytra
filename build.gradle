plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
	archivesName = project.project_id
}

repositories {
	flatDir {
        dirs "local_maven"
    }
}

loom {
	mods {
		"${project.project_id}" {
			sourceSet sourceSets.main
		}
	}
}

sourceSets {
	main.java.srcDirs = ["src/main/java"]
	main.resources.srcDirs = ["src/main/resources"]
}


dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.properties["fabric-api"]}"
	modImplementation "eu.midnightdust:midnightlib:${project.properties["midnightlib"]}"
	include "eu.midnightdust:midnightlib:${project.properties["midnightlib"]}"
}


processResources {
	inputs.property "version", project.version
	inputs.property "project_id", project.project_id
	inputs.property "java_version", project.java_version
	inputs.property "minecraft_compatible_range", Boolean.parseBoolean("${project.enforce_range}") ? project.minecraft_compatible_range : "\"*\""
	
	filesMatching("fabric.mod.json") {
		expand (
			project_id: inputs.properties.project_id,
			version: inputs.properties.version,
			java_version: inputs.properties.java_version,
			minecraft_compatible_range: inputs.properties.minecraft_compatible_range,
		)
	}
	
	filesMatching("*.mixins.json") {
		expand (
			java_version: JavaVersion.toVersion(project.java_version).toString().replace(".", "_"),
		)
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = Integer.parseInt(project.java_version)
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	// withSourcesJar()
	
	sourceCompatibility = JavaVersion.toVersion(project.java_version)
	targetCompatibility = JavaVersion.toVersion(project.java_version)
}

jar {
	inputs.property "archivesName", project.base.archivesName
	
	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}" }
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.base.archivesName
			from components.java
		}
	}
	
	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}